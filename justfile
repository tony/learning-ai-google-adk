# justfile for google-adk-example
# https://just.systems/

set shell := ["bash", "-uc"]

# File patterns
py_files := "find . -type f -not -path '*/\\.*' | grep -i '.*[.]py$' 2> /dev/null"
all_files := "find . -type f -not -path '*/\\.*' | grep -i '.*[.]py$\\|.*[.]md$\\|.*[.]toml$' 2> /dev/null"

# List all available commands
default:
    @just --list

# Launch ADK web UI (serves all agents)
[group: 'dev']
start:
    uv run adk web .

# Alias for start
[group: 'dev']
dev:
    just start

# Run tests with pytest
[group: 'test']
test *args:
    uv run py.test {{ args }}

# Watch files and run tests on change (requires entr)
[group: 'test']
watch-test:
    #!/usr/bin/env bash
    set -euo pipefail
    if command -v entr > /dev/null; then
        {{ all_files }} | entr -c just test
    else
        just test
        just _entr-warn
    fi

# Format code with ruff
[group: 'lint']
ruff-format:
    uv run ruff format .

# Run ruff linter
[group: 'lint']
ruff:
    uv run ruff check .

# Run ruff linter with auto-fixes
[group: 'lint']
ruff-fix:
    uv run ruff check . --fix --show-fixes

# Watch files and run ruff on change
[group: 'lint']
watch-ruff:
    #!/usr/bin/env bash
    set -euo pipefail
    if command -v entr > /dev/null; then
        {{ py_files }} | entr -c just ruff
    else
        just ruff
        just _entr-warn
    fi

# Run mypy type checker
[group: 'lint']
mypy:
    uv run mypy google_search_agent content_generator content_generator_agent

# Watch files and run mypy on change
[group: 'lint']
watch-mypy:
    #!/usr/bin/env bash
    set -euo pipefail
    if command -v entr > /dev/null; then
        {{ py_files }} | entr -c just mypy
    else
        just mypy
        just _entr-warn
    fi

# Run full development workflow (format, test, lint, type-check)
[group: 'dev']
check:
    just ruff-format
    just test
    just ruff
    just mypy

# Sync dependencies
[group: 'dev']
sync:
    uv sync

[private]
_entr-warn:
    @echo "----------------------------------------------------------"
    @echo "     ! File watching functionality non-operational !      "
    @echo "                                                          "
    @echo "Install entr(1) to automatically run tasks on file change."
    @echo "See https://eradman.com/entrproject/                      "
    @echo "----------------------------------------------------------"
